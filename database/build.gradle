plugins {
    id 'io.quarkus' version "$quarkusVersion"
}

def static randomPort() {
    try {
        ServerSocket server = new ServerSocket()
        server.setReuseAddress(true)
        server.bind(new InetSocketAddress(0))
        int port = server.getLocalPort()
        server.close()
        return port
    } catch (IOException e) {
        throw new IOException('Failed to find random free port', e)
    }
}

dependencies {
    implementation platform("io.quarkus:quarkus-bom:$quarkusVersion")
    implementation platform("org.trellisldp:trellis-bom:$trellisVersion")

    // Quarkus Application
    implementation 'io.quarkus:quarkus-agroal'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-jsonb'
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-security'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-jwt'
    implementation 'io.quarkus:quarkus-smallrye-reactive-messaging'

    // Trellis Application
    implementation "org.trellisldp:trellis-app"
    implementation "org.trellisldp:trellis-cdi"

    // Trellis Auth
    implementation "org.trellisldp:trellis-jwt"
    implementation "org.trellisldp:trellis-webac"

    // Trellis components
    implementation "org.trellisldp:trellis-api"
    implementation "org.trellisldp:trellis-audit"
    implementation "org.trellisldp:trellis-cache"
    implementation "org.trellisldp:trellis-constraint"
    implementation "org.trellisldp:trellis-file"
    implementation "org.trellisldp:trellis-http"
    implementation "org.trellisldp:trellis-jdbc"
    implementation "org.trellisldp:trellis-jena"
    implementation "org.trellisldp:trellis-namespace"
    implementation "org.trellisldp:trellis-notification-jsonb"
    implementation "org.trellisldp:trellis-rdfa"
    implementation "org.trellisldp:trellis-reactive"
    implementation "org.trellisldp:trellis-vocabulary"

    implementation "com.github.spullara.mustache.java:compiler:$mustacheVersion"
    implementation "com.google.guava:guava"
    implementation("org.apache.jena:jena-arq:$jenaVersion")
    implementation("org.apache.jena:jena-commonsrdf:$jenaVersion")
    implementation "org.slf4j:slf4j-api"
    implementation "org.yaml:snakeyaml"


    runtime "jakarta.activation:jakarta.activation-api"
    runtime "jakarta.xml.bind:jakarta.xml.bind-api"

    testImplementation "com.opentable.components:otj-pg-embedded:$otjPgVersion"
    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.jboss.resteasy:resteasy-client"
    testImplementation "org.apache.commons:commons-text:$commonsTextVersion"
    testImplementation "org.trellisldp:trellis-test"
}

test {
    if (!project.hasProperty("externalPgsql")) {
        def port = randomPort()
        systemProperty 'testing.pgsql.port', port
        systemProperty 'quarkus.datasource.username', 'postgres'
        systemProperty 'quarkus.datasource.password', 'postgres'
        systemProperty 'quarkus.datasource.jdbc.url', "jdbc:postgresql://localhost:$port/postgres"
    } else {
        systemProperty 'testing.external.pgsql', 'true'
    }

    systemProperty 'com.arjuna.ats.arjuna.objectstore.objectStoreDir', "$buildDir/data/ObjectStore"
    systemProperty 'trellis.file.memento-path', "$buildDir/data/mementos"
    systemProperty 'trellis.file.binary-path', "$buildDir/data/binaries"
    systemProperty 'mp.jwt.verify.publickey.location', 'https://www.trellisldp.org/tests/jwks.json'
}

